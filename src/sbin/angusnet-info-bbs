#!/usr/bin/env perl

# this is intended to be run through in.telnetd as a replacement
# for login with telnetd running as nobody, example inetd.conf:
# telnet stream tcp nowait nobody /usr/sbin/tcpd /usr/sbin/in.telnetd -L /usr/local/sbin/angusnet-info-bbs

# debian packages:
#   cowsay bsdgames animals libfile-slurp-perl telnetd

use strict;
use warnings;
use v5.10;
use autodie;

use Data::Dumper;
use File::Basename;
use Getopt::Long;

main();

sub main {
    # ignored for now - just here for compat with in.telnetd
    my %args = parse_args();
    my $bbs = BBS->new;

    $bbs->run_session;
    exit(0);
}

sub parse_args {
    my %args;

    GetOptions(
        "p" => \$args{preserve},
        "h=s" => \$args{host},
    ) or usage();

    usage() if @ARGV;

    return %args;
}

sub usage {
    my $basename = basename($0);

    say STDERR "Usage:";
    say STDERR "  $basename [-p] [-h <hostname>] <username>";
    say STDERR "  like /usr/bin/login but all switches are ignored";
    say STDERR "  specifying a username generates a fatal error";

    exit(1);
}

package BBS;

use strict;
use warnings;
use v5.10;
use autodie;

use Data::Dumper;
use File::Slurp;

use constant VERSION => '0.0.1';
use constant INFO_PATH => '/etc/local/angusnet/nodeinfo.txt';
use constant PROMPT => '? for help> ';
use constant COMMAND_HANDLERS => {
    '?' => \&command_help,
    help => \&command_help,
    bye => \&command_bye,
    exit => \&command_bye,
    quit => \&command_bye,
    logout => \&command_bye,
    info => \&command_info,
    show => \&command_show,
    cow => \&command_cow,
    game => \&command_game,
};
use constant SHOW_PROGRAMS => {
    'rip-neighbors' => '/usr/local/bin/angusnet-show-ip-rip-status',
    'rip-table' => '/usr/local/bin/angusnet-show-ip-rip',
    'ip-neighbors' => '/usr/local/bin/angusnet-show-ampr-neighbors',
    'ip-route' => '/usr/local/bin/angusnet-show-ampr-route',
    'netrom-neighbors' => '/usr/local/bin/angusnet-show-netrom-neighbors',
    'netrom-nodes' => '/usr/local/bin/angusnet-show-netrom-nodes',
};
use constant COW_PROGRAMS => {
    say => '/usr/games/cowsay',
    think => '/usr/games/cowthink',
};
use constant GAME_PROGRAMS => {
    adventure => '/usr/games/adventure',
    animals => '/usr/games/animals',
    gofish => '/usr/games/go-fish',
    hangman => '/usr/games/hangman',
    trek => '/usr/games/trek',
};

sub new {
    my ($package) = @_;
    return bless({}, $package);
}

sub run_session {
    my ($self) = @_;
    our $SESSION_RUNNING = 1;
    our $VERSION;

    # more puts less load on the network
    $ENV{PAGER} = "more";

    say "AngusBBS v", VERSION;
    say "Enable linemode in your telnet client for best experience.";

    while($SESSION_RUNNING) {
        my (@input) = prompt();

        # user pushed enter and had nothing but white space
        next unless @input;
        my ($command, @args) = @input;
        # read error
        last unless defined $command;

        my $handler = COMMAND_HANDLERS->{$command};
        if (defined $handler) {
            $handler->(@args);
        } else {
            bad_command($command);
        }
    }
}

sub prompt {
    our $SESSION_RUNNING = 1;

    print PROMPT;

    my $input = <STDIN>;
    return undef unless defined $input;

    $input =~ s/^\s+//;
    $input =~ s/\s+$//;

    return split(/\s+/, $input);
}

sub bad_command {
    my ($command) = @_;
    say "Error: invalid command '$command'";
    say '';
}

sub command_help {
    say "Commands:";
    say "  ? or help: Get this help info";
    say "  bye: logout";
    say "  info: get a text description of this system";
    say "  show: get information on the status of this system";
    say "  cow: make a cow do things";
    say "  game: pick a game to play";
    say '';
}

sub command_bye {
    our $SESSION_RUNNING = 0;
    say "Logging out";
}

sub command_info {
    if (-e INFO_PATH) {
        say read_file(INFO_PATH);
    } else {
        say "no info available";
    }
}

sub show_program {
    my (@command) = @_;
    system(@command);
    say '';
    return;
}

sub command_show {
    my ($to_show) = @_;

    unless (defined $to_show) {
        command_show__usage();
        return;
    }

    my $program = SHOW_PROGRAMS->{$to_show};
    unless (defined $program) {
        command_show__usage();
        return;
    }

    show_program($program);
}

sub command_show__usage {
    say "Usage:";

    for my $name (sort keys SHOW_PROGRAMS->%*) {
        say "  show $name";
    }
}

sub command_cow {
    my (@args) = @_;

    if (@args < 2) {
        command_cow__usage();
        return;
    };

    my $verb = shift @args;
    my $text = join(' ', @args);
    my $program = COW_PROGRAMS->{$verb};

    if (defined $program) {
        system($program, $text);
    } else {
        command_cow__usage();
    }

}

sub command_cow__usage {
    say "Usage: ";
    say "  cow say <something to say>";
    say "  cow think <something to think>";

    return;
}

sub command_game {
    my ($game_name) = @_;

    unless (defined $game_name) {
        command_game__usage();
        return;
    }

    my $program = GAME_PROGRAMS->{$game_name};
    unless (defined $program) {
        say "Invalid game name '$game_name'";
        command_game__usage();
        return;
    }

    system($program);
}

sub command_game__usage {
    say "Usage: game <game name>";
    say "Available games:";

    for my $name (sort keys GAME_PROGRAMS->%*) {
        say "  $name";
    }

    return;
}

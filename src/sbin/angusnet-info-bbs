#!/usr/bin/env perl

use strict;
use warnings;
use v5.10;
use autodie;

main();

sub main {
    $/ = "\r\n";

    BBS->run();
}

package BBS;

use strict;
use warnings;
use v5.10;
use autodie;

use Net::Server::INET;

use base qw(Net::Server::INET);

use constant WELCOME_FILE => '/etc/local/angusnet/node-info.welcome.txt';
use constant PROMPT => '? for help>';
use constant COMMAND_HANDLERS => {
    '?' => \&command_help,
};

sub process_request {
    my ($self) = @_;
    my $command;

    while(defined($command = prompt())) {
        if ($command eq 'bye') {
            last;
        }

        my $handler = COMMAND_HANDLERS->{$command};

        if (defined $handler) {
            $handler->();
        } else {
            bad_command($command);
        }
    }

    say "Goodbye";
    exit(0);
}

sub prompt {
    print PROMPT, ' ';

    my $input = <STDIN>;
    return undef unless defined $input;

    chomp($input);
    return $input;
}

sub bad_command {
    my ($command) = @_;
    say "Error: invalid command '$command'";
    say '';
}

sub command_help {
    say "Commands:";
    say "? Get this help info";
    say "bye logout";
    say '';
}
